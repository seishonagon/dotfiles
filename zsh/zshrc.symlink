# try to profile time
# (https://blog.askesis.pl/post/2017/04/how-to-debug-zsh-startup-time.html)
# zmodload zsh/zprof
# shortcut to this dotfiles path is $DOTFILE
export DOTFILE=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Documents/code/

# new location for antibody plugins
export ANTIBODY_HOME=~/Library/antibody


# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILE/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load antibody
# static load, faster but needs to be updated with each new plugin
# source $DOTFILE/antibody/zsh_plugins.sh
# dynamic load, while I figure it out
source <(antibody init)
# change antibody configuration file

antibody bundle < $DOTFILE/antibody/zsh_plugins.txt

# load extra plugins syntax highlighting, touchbar and z
# source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source $DOTFILE/plugins/zsh-apple-touchbar
. /usr/local/etc/profile.d/z.sh

# initialize autocomplete here, otherwise functions won't be loaded
# hack to speed up loading https://carlosbecker.com/posts/speeding-up-zsh/
# autoload -U compinit
# compinit
autoload -Uz compinit
if [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.zcompdump) ]; then
	compinit
else
	compinit -C
fi
# end hack

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

fpath=(/usr/local/share/zsh-completions $fpath)

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# try to lower startup time (see top of file)
# zprof

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

